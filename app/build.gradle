apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.timfreiheit.resourceplaceholders'
apply from: 'download.gradle'

android {
    compileSdkVersion 33
    buildToolsVersion '32.0.0'
    kotlinOptions {
        jvmTarget = "11"
    }
    namespace 'io.legado.app'

    defaultConfig {
        applicationId "org.sean.mlbook"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 55
        versionName "23.06.28"


        buildConfigField("String", "Cronet_Version", "\"$CronetVersion\"")

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.incremental"     : "true",
                        "room.expandProjection": "true",
                        "room.schemaLocation"  : "$projectDir/schemas".toString()
                ]
            }
        }
    }
    buildFeatures {
        viewBinding true
        compose true
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
            versionNameSuffix 'debug'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 11
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.1.1'
    }

    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    tasks.withType(JavaCompile) {
        //options.compilerArgs << "-Xlint:unchecked"
    }
}

resourcePlaceholders {
    files = ['xml/shortcuts.xml']
}

kapt {
    arguments {
        arg("room.schemaLocation", "$projectDir/schemas")
    }
}

dependencies {
    coreLibraryDesugaring('com.android.tools:desugar_jdk_libs:2.0.3')
    //kotlin
    //noinspection GradleDependency
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")
    implementation("org.jetbrains.kotlin:kotlin-reflect:$kotlin_version")
    def coroutines_version = '1.6.1-native-mt'
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version")

    //androidX
    implementation('androidx.core:core-ktx:1.7.0')
    implementation('androidx.appcompat:appcompat:1.4.1')
    implementation('androidx.activity:activity-ktx:1.4.0')
    implementation('androidx.fragment:fragment-ktx:1.4.1')
    implementation('androidx.preference:preference-ktx:1.2.0')
    implementation('androidx.constraintlayout:constraintlayout:2.1.3')
    implementation('androidx.swiperefreshlayout:swiperefreshlayout:1.1.0')
    implementation('androidx.viewpager2:viewpager2:1.0.0')
    implementation('androidx.webkit:webkit:1.4.0')

    //google
    implementation('com.google.android.material:material:1.6.0')
    implementation('com.google.android.flexbox:flexbox:3.0.0')
    implementation('com.google.code.gson:gson:2.9.0')

    //lifecycle
    def lifecycle_version = '2.4.1'
    implementation("androidx.lifecycle:lifecycle-common-java8:$lifecycle_version")

    //compose
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.4.0'
    // Compose Material Design
    implementation 'androidx.compose.material:material:1.1.1'
    // Animations
    implementation 'androidx.compose.animation:animation:1.1.1'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.1.1'
    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.1.1'

    //media
    implementation("androidx.media:media:1.6.0")
    def exoplayer_version = '2.18.7'
    implementation("com.google.android.exoplayer:exoplayer-core:$exoplayer_version")
    implementation("com.google.android.exoplayer:extension-okhttp:$exoplayer_version")

    //Splitties
    def splitties_version = '3.0.0'
    implementation("com.louiscad.splitties:splitties-appctx:$splitties_version")
    implementation("com.louiscad.splitties:splitties-systemservices:$splitties_version")
    implementation("com.louiscad.splitties:splitties-views:$splitties_version")

    //room
    def room_version = '2.4.2'
    implementation("androidx.room:room-runtime:$room_version")
    implementation("androidx.room:room-ktx:$room_version")
    kapt("androidx.room:room-compiler:$room_version")
    testImplementation("androidx.room:room-testing:$room_version")

    //liveEventBus
    implementation('io.github.jeremyliao:live-event-bus-x:1.8.0')

    implementation('org.jsoup:jsoup:1.14.3')
    implementation('com.jayway.jsonpath:json-path:2.7.0')
    implementation('cn.wanghaomiao:JsoupXpath:2.5.1')
    implementation(project(path: ':epublib'))

    //JS rhino
    //implementation('com.github.gedoor:rhino-android:1.8')
    implementation(fileTree(dir: 'lib', include: ['rhino-*.jar']))

    implementation('com.squareup.okhttp3:okhttp:4.9.3')
//    implementation(fileTree(dir: 'cronetlib', include: ['*.jar', '*.aar']))

    //Glide
    def glideVersion = "4.13.2"
    implementation("com.github.bumptech.glide:glide:$glideVersion")
    kapt("com.github.bumptech.glide:compiler:$glideVersion")

    //webServer
    def nanoHttpdVersion = "2.3.1"
    implementation("org.nanohttpd:nanohttpd:$nanoHttpdVersion")
    implementation("org.nanohttpd:nanohttpd-websocket:$nanoHttpdVersion")

    implementation('com.github.jenly1314:zxing-lite:2.1.1')

    implementation('com.jaredrummler:colorpicker:1.1.0')

    //apache
    implementation('org.apache.commons:commons-text:1.9')

    //MarkDown
    def markwonVersion = "4.6.2"
    implementation("io.noties.markwon:core:$markwonVersion")
    implementation("io.noties.markwon:image-glide:$markwonVersion")
    implementation("io.noties.markwon:ext-tables:$markwonVersion")
    implementation("io.noties.markwon:html:$markwonVersion")

    implementation('com.github.liuyueyi.quick-chinese-transfer:quick-transfer-core:0.2.3')

    implementation('cn.hutool:hutool-crypto:5.8.0.M1')

    //renderscriptToolkit
    implementation('com.github.android:renderscript-intrinsics-replacement-toolkit:b6363490c3')

    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.github.mylhyl:Android-CircleDialog:5.3.11'
    implementation 'com.blankj:utilcodex:1.30.6'

    implementation("com.google.android.gms:play-services-ads:22.1.0")
    implementation('com.google.android.gms:play-services-cronet:17.0.1')
}
